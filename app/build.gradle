apply plugin: 'com.android.application'
//Kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
//Realm
apply plugin: 'realm-android'

def VERSION_CODE = 1
def VERSION_NAME = "1.0.0"
def PACKAGE_NAME = "com.kazakago.cleanarchitecture"

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 17
        targetSdkVersion 26
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //Vector Drawable Support
        vectorDrawables.useSupportLibrary = true
        //MultiDex
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }
    }
    flavorDimensions "app"
    productFlavors {
        production {
            dimension "app"
        }
        staging {
            dimension "app"
            applicationId "${PACKAGE_NAME}.staging"
            versionName "${VERSION_NAME} staging"
        }
    }
    dataBinding {
        //Need for Android Data-Binding
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    //Need for Stetho-Realm
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //v7 AppCompat Support Library
    implementation 'com.android.support:appcompat-v7:26.1.0'
    //Design Support Library
    implementation 'com.android.support:design:26.1.0'
    //Data-Binding for Kotlin
    kapt 'com.android.databinding:compiler:3.0.0'
    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    //Moshi
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
    //Kodein
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-android:4.1.0'
    //Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'
    //PaperParcel
    implementation 'nz.bradcampbell:paperparcel:2.0.4'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.4'
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.4'
    //Android-State
    implementation 'com.evernote:android-state:1.1.5'
    kapt 'com.evernote:android-state-processor:1.1.5'

    //Stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    //Stetho-Realm
    debugImplementation 'com.uphyca:stetho_realm:2.0.0'

    //JUnit
    testImplementation 'junit:junit:4.12'
    //Hamcrest
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    //Espresso
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //Hamcrest
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}
