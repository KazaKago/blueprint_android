apply plugin: 'com.android.application'
//Android APT for Dagger2
apply plugin: 'android-apt'
//Retrolambda
apply plugin: 'me.tatarka.retrolambda'
//Realm
apply plugin: 'realm-android'
//Spoon
apply plugin: 'spoon'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        applicationId "com.kazakago.cleanarchitecture"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        //Vector Drawable Support
        vectorDrawables.useSupportLibrary = true
        //Need for Android-Test
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        //Need for Retrolambda
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }
    }
    dataBinding {
        //Need for Android Data-Binding
        enabled = true
    }
}

repositories {
    maven {
        //Need for Stetho-Realm
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
    maven {
        //Need for Icepick
        url "https://clojars.org/repo/"
    }
}

spoon {
    // for debug output
    debug true
    // To execute the tests device by device
    sequential true
    // To grant permissions to Android M >= devices
    grantAllPermissions true
    // Disable animated gif generation
    noAnimations true
    // Set maximum execution time per test in seconds (10min default)
    adbTimeout 60
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //v7 AppCompat Support Library
    compile 'com.android.support:appcompat-v7:25.0.1'
    //Design Support Library
    compile 'com.android.support:design:25.0.1'
    //Rx
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    //Dagger2
    compile 'com.google.dagger:dagger:2.2'
    apt 'com.google.dagger:dagger-compiler:2.2'
    provided 'javax.annotation:jsr250-api:1.0'
    //Icepick
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
    //ModelMapper
    compile 'org.modelmapper:modelmapper:0.7.5'
    //Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    //Stetho
    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    //Stetho-Realm
    debugCompile 'com.uphyca:stetho_realm:2.0.0'

    //JUnit
    testCompile 'junit:junit:4.12'
    //Hamcrest
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    //Dagger2
    testCompile 'com.google.dagger:dagger:2.2'
    testApt 'com.google.dagger:dagger-compiler:2.2'
    testProvided 'javax.annotation:jsr250-api:1.0'

    //Espresso
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //Spoon
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.0'
    //Hamcrest
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    //Dagger2
    androidTestCompile 'com.google.dagger:dagger:2.2'
    androidTestApt 'com.google.dagger:dagger-compiler:2.2'
    androidTestProvided 'javax.annotation:jsr250-api:1.0'
}
